// Top-level build file where you can add configuration options common to all sub-projects/modules.

//def keystorePropertiesFile = rootProject.file("keystore.properties")
//def keystoreProperties = new Properties()
//keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

buildscript {
    ext.kotlin_version = '1.4.10'

    ext.libraries = [
            navigation_safe_args__version: '2.3.0'
    ]

    repositories {
        jcenter()
        google()
        maven { url 'https://dl.bintray.com/kotlin/kotlin-eap' }
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:4.1.0-rc03'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "androidx.navigation:navigation-safe-args-gradle-plugin:$libraries.navigation_safe_args__version"

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

allprojects {
    repositories {
        google()
        jcenter()
        maven { url 'https://jitpack.io' }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

project.ext {
    global_compileSdkVersion = 30
    global_targetSdkVersion = 30
    global_minSdkVersion = 21
    global_buildToolsVersion = "30.0.2"
}

// This adds the configuration for all modules
subprojects {
    afterEvaluate {
        def isAndroidProject = project.plugins.hasPlugin('android')
        def isLibraryProject = project.plugins.hasPlugin('android-library')
        if (isAndroidProject || isLibraryProject) {
            android {
                compileSdkVersion global_compileSdkVersion
                buildToolsVersion global_buildToolsVersion

                buildFeatures {
                    viewBinding true
                }

                defaultConfig {
                    minSdkVersion global_minSdkVersion
                    targetSdkVersion global_targetSdkVersion
                }

                compileOptions {
                    sourceCompatibility JavaVersion.VERSION_1_8
                    targetCompatibility JavaVersion.VERSION_1_8
                }

                kotlinOptions {
                    jvmTarget = '1.8'
                }

                lintOptions {
                    disable 'MissingTranslation'
                    abortOnError false
                }

                sourceSets {
                    main.java.srcDirs += 'src/main/kotlin'
                }

                if (isAndroidProject) {
//                    signingConfigs {
//                        release {
//                            keyAlias keystoreProperties['alias']
//                            keyPassword keystoreProperties['keyPassword']
//                            storeFile file('release_key.jks')
//                            storePassword keystoreProperties['storePassword']
//                        }
//                    }

                    buildTypes {
                        release {
//                            signingConfig signingConfigs.release
                            minifyEnabled false
                            proguardFiles getDefaultProguardFile('proguard-android.txt'), "${project.projectDir}/proguard-project.txt"
                        }
                    }

                    applicationVariants.all { variant ->
                        variant.outputs.each { output ->

                            def isRelease = variant.buildType.name == 'release'
                            if (isRelease) {
                                // This sets output file path to "root/apk-release". Previously in early gradle versions, you needed to just modify outputFile,
                                // but this is now not possible and is the only way
                                def outputDirPath = new File("${project.rootDir.absolutePath}/apk-release")
                                variant.packageApplicationProvider.get().outputDirectory = outputDirPath
                                output.outputFileName = output.outputFile.name.replace(".apk", "-v${variant.versionName}-${variant.versionCode}.apk")
                            }
                        }
                    }
                }
            }

            dependencies {
                implementation 'androidx.core:core-ktx:1.3.1'
                implementation 'androidx.constraintlayout:constraintlayout:2.0.1'
                implementation "androidx.swiperefreshlayout:swiperefreshlayout:1.1.0"

                implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.3.9'
                implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.3.9'

                implementation 'androidx.documentfile:documentfile:1.0.1'

                // Permissions
                implementation 'com.markodevcic.peko:peko:2.1.1'

                // Preferences
                implementation 'androidx.preference:preference-ktx:1.1.1'
                implementation "com.github.skydoves:colorpickerpreference:2.0.1"

                // Theming
                implementation 'com.github.DeweyReed:Theme:develop-SNAPSHOT'

                // Inline activity result
                implementation 'com.afollestad.inline-activity-result:coroutines:0.2.0'

                implementation 'androidx.appcompat:appcompat:1.2.0'
                implementation 'com.github.ajalt.reprint:core:3.3.0@aar'
                implementation 'com.google.android.material:material:1.2.0'
                implementation 'com.duolingo.open:rtl-viewpager:1.0.3'

                // Useful extensions
                implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.2.0'
                implementation 'androidx.lifecycle:lifecycle-livedata-ktx:2.2.0'

                // Navigation
                implementation 'androidx.navigation:navigation-fragment-ktx:2.3.0'
                implementation 'androidx.navigation:navigation-ui-ktx:2.3.0'

                // Network stuff
                implementation 'com.google.code.gson:gson:2.8.6'
                implementation 'com.squareup.retrofit2:converter-gson:2.9.0'
                implementation('com.squareup.retrofit2:retrofit:2.9.0') {
                    exclude group: 'squareup.okhttp3', module: 'okhttp'
                }
                implementation 'com.squareup.okhttp3:okhttp:4.7.2'
                implementation 'com.squareup.okhttp3:logging-interceptor:4.7.2'
                implementation 'com.squareup.retrofit2:converter-scalars:2.5.0'

                // Database stuff
                implementation "androidx.room:room-runtime:2.2.5"
                kapt "androidx.room:room-compiler:2.2.5"
                implementation 'androidx.room:room-ktx:2.2.5'

                // Dependency injection
                implementation "org.koin:koin-core:2.2.0-alpha-1"
                implementation "org.koin:koin-android:2.2.0-alpha-1"
                implementation "org.koin:koin-androidx-viewmodel:2.2.0-alpha-1"

                // Images
                implementation "io.coil-kt:coil:0.13.0"
                implementation "io.coil-kt:coil-video:0.13.0"

                if (isAndroidProject) {
                    implementation project(':common')
                }
            }
        }
    }
}
